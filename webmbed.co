"use strict"

export webmbed = {}

# Magic separator between the end of the jpg and the start of the
# video. Would be nice if I could use a regex against an ArrayBuffer.
MAGIC = => c.charCodeAt 0 for c of '\xff\xd9webmbed'

# takes a file/Blob input and calls cb(err, Blob video)
webmbed.extract = !(file, cb) ->
  f = new FileReader
    &readAsArrayBuffer file
  <-! f.addEventListener \load

  data = new Uint8Array @result

  pos = -1
  :search for i til data.length
    for c, j of MAGIC
      continue search if data[i + j] is not c
    pos = i
    break

  if pos is -1
    cb new Error "Couldn't find anything MAGICAL about this image"
  else
    cb do
      null
      new Blob do
        [data.subarray pos + MAGIC.length]
        type: \video/webm


